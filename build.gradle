apply from: file("${rootDir}/gradle/project.gradle")

subprojects {
    group = 'core.demo'
    version = '1.0.0'

    repositories {
        maven {
            url 'https://neowu.github.io/maven-repo/'
            content {
                includeGroupByRegex 'core\\.framework.*'
            }
        }
    }
}

def coreNGVersion = '7.0.0'
def hsqlVersion = '2.5.0'

configure(subprojects.findAll { it.name.matches('.*-service') }) {
    apply from: file("${rootDir}/gradle/app.gradle")
    dependencies {
        implementation "core.framework:core-ng:${coreNGVersion}"
        testImplementation "core.framework:core-ng-test:${coreNGVersion}"
    }
}

configure([project(':bo-order-site'),
           project(':order-site')]) {
    apply from: file("${rootDir}/gradle/app.gradle")
    dependencies {
        implementation "core.framework:core-ng:${coreNGVersion}"
        testImplementation "core.framework:core-ng-test:${coreNGVersion}"
    }
}

configure(subprojects.findAll { it.name.matches('.*-interface') }) {
    dependencies {
        implementation "core.framework:core-ng-api:${coreNGVersion}"
    }
}


project(':order-service') {
    dependencies {
        implementation project(':order-service-interface')
        implementation project(':order-service-bo-ajax-interface')
        implementation project(':order-service-ajax-interface')
        implementation "core.framework:core-ng-mongo:${coreNGVersion}"
        runtimeOnly 'mysql:mysql-connector-java:8.0.15'
    }
}

project(':bo-order-site') {
    dependencies {
        implementation project(':order-service-interface')
        implementation project(':order-service-bo-ajax-interface')
    }
}

project(':order-site') {
    dependencies {
        implementation project(':order-service-interface')
        implementation project(':order-service-ajax-interface')
    }
}

configure(subprojects.findAll { it.name.endsWith('-db-migration') }) {
    apply from: file("${rootDir}/gradle/db-migration.gradle")

    dependencies {
        runtimeOnly 'mysql:mysql-connector-java:8.0.17'
    }
}

configure(subprojects.findAll { it.name.endsWith('-mongo-migration') }) {
    apply from: file("${rootDir}/gradle/app.gradle")
    dependencies {
        implementation "core.framework:core-ng:${coreNGVersion}"
        implementation "core.framework:core-ng-mongo:${coreNGVersion}"
    }
    tasks.register('mongoMigrate') {
        dependsOn run
    }
}